if !has('nvim') && has('vim_starting') | set encoding=utf-8 nobomb | endif
scriptencoding utf-8

"
"   ██╗   ██╗ ██╗ ███╗   ███╗ ██████╗   ██████╗
"   ██║   ██║ ██║ ████╗ ████║ ██╔══██╗ ██╔════╝
"   ██║   ██║ ██║ ██╔████╔██║ ██████╔╝ ██║
"   ╚██╗ ██╔╝ ██║ ██║╚██╔╝██║ ██╔══██╗ ██║
" ██╗╚████╔╝  ██║ ██║ ╚═╝ ██║ ██║  ██║ ╚██████╗
" ╚═╝ ╚═══╝   ╚═╝ ╚═╝     ╚═╝ ╚═╝  ╚═╝  ╚═════╝
"

" ============================================================================
" Settings vars
" ============================================================================

" Just for debugging
let g:dko_rtp_original = &runtimepath

" Used by anything that needs to change/use loc list
let g:dko_loc_list_height = 10

" This must be a g: variable to access with the Plug command
let g:fzf_dir = isdirectory('/usr/local/opt/fzf')
      \ ? '/usr/local/opt/fzf'
      \ : isdirectory(expand('~/.fzf'))
      \ ? '~/.fzf'
      \ : ''

" no longer using comma so I can reverse repeat fFtT searches
" no longer using \ so I can type escaped chars :/
" needs to be before bundle so plugins map correctly
let g:mapleader = "\<Space>"

" Plugin settings
let g:dko_use_completion = has('nvim') && has('python3')
let g:dko_use_js_langserver = executable('javascript-typescript-stdio')
let g:dko_use_tern_lsp = g:dko_use_completion && executable('npm')
let g:dko_use_composer = g:dko_use_completion && executable('composer')

" ============================================================================
" My autocmd block - Clear vimrc group's autocmds if reloading
" ============================================================================

augroup vimrc
  autocmd!
augroup END

" ============================================================================
" My defaults
" May be overridden by **/plugins, after/plugins and **/ftplugins
" ============================================================================

set modeline

" ----------------------------------------------------------------------------
" Clipboard -- use os clipboard
" ----------------------------------------------------------------------------

if empty($SSH_CONNECTION) && has('clipboard')
  set clipboard^=unnamed                " Use vim global clipboard register
  if has('unnamedplus') || has('nvim')  " Use system clipboard register
    set clipboard^=unnamedplus
  endif
endif

" ----------------------------------------------------------------------------
" Display
" ----------------------------------------------------------------------------

set title                             " wintitle = filename - vim

" this is already set by modern terminal and removed from nvim
"set ttyfast

" no beeps or flashes
set visualbell

set number
set numberwidth=5

" show context around current cursor position
set scrolloff=8
set sidescrolloff=16

set textwidth=78
" the line will be right after column 80, &tw+3
set colorcolumn=+3
set colorcolumn+=120
set cursorline

set synmaxcol=512                     " don't syntax highlight long lines

" ------------------------------------
" Tab line
" ------------------------------------

set showtabline=0                     " start OFF, toggle =2 to show tabline

" ------------------------------------
" Status line
" ------------------------------------

set laststatus=2                      " always show all statuslines

" ------------------------------------
" Command line
" ------------------------------------

" This is slow on some terminals and often gets hidden by msgs so leave it off
set noshowcmd

set noshowmode                        " don't show -- INSERT -- in cmdline

" ----------------------------------------------------------------------------
" Input
" ----------------------------------------------------------------------------

" Enable mouse
set mouse=a

" Typing key combos
set notimeout
set ttimeout

" ----------------------------------------------------------------------------
" Wild and file globbing stuff in command mode
" ----------------------------------------------------------------------------

set browsedir=buffer                  " browse files in same dir as open file
set wildmenu                          " Enhanced command line completion.
set wildmode=list:longest,full        " Complete files using a menu AND list
let &wildignorecase = v:version >= 704

" wildignore prevents things from showing up in cmd completion
" It's for things you'd NEVER open in Vim, like caches and binary files
" @see https://github.com/tpope/vim-fugitive/issues/121#issuecomment-38720847
" @see https://github.com/kien/ctrlp.vim/issues/63
" @see https://github.com/tpope/vim-vinegar/issues/61#issuecomment-167432416
" @see http://tbaggery.com/2011/08/08/effortless-ctags-with-git.html#comment-1396330403
"
" So don't do this! There are cases where you'd edit them or their contents
"set wildignore+=.git
"set wildignore+=.hg,.svn
"set wildignore+=tags
"set wildignore+=*.manifest

" Binary
set wildignore+=*.aux,*.out,*.toc
set wildignore+=*.o,*.obj,*.exe,*.dll,*.jar,*.pyc,*.rbc,*.class
set wildignore+=*.ai,*.bmp,*.gif,*.ico,*.jpg,*.jpeg,*.png,*.psd,*.webp
set wildignore+=*.avi,*.m4a,*.mp3,*.oga,*.ogg,*.wav,*.webm
set wildignore+=*.eot,*.otf,*.ttf,*.woff
set wildignore+=*.doc,*.pdf
set wildignore+=*.zip,*.tar.gz,*.tar.bz2,*.rar,*.tar.xz
" Cache
set wildignore+=*/vendor/gems/*,*/vendor/cache/*,*/.bundle/*,*.gem
set wildignore+=.sass-cache
set wildignore+=npm-debug.log
" Compiled
set wildignore+=*.marko.js
set wildignore+=*.min.*,*-min.*
" Temp/System
set wildignore+=*.*~,*~
set wildignore+=*.swp,.lock,.DS_Store,._*,tags.lock

" ----------------------------------------------------------------------------
" File saving
" ----------------------------------------------------------------------------

" reload files if they were edited elsewhere?
"set autoread

set fileformats=unix,mac,dos
" Not modifiable if no window (e.g. resourcing vimrc)
if !&modifiable | set fileformat=unix | endif

" ----------------------------------------------------------------------------
" Temp dirs
" ----------------------------------------------------------------------------

" ----------------------------------------------------------------------------
" Writing swaps
" ----------------------------------------------------------------------------

" default is on, explicitly set on here.
" pros: prevent editing stale copy of same file in two vim instances
set swapfile

" ----------------------------------------------------------------------------
" Write backups
" ----------------------------------------------------------------------------

" Use backup files when writing (create new file, replace old one with new
" one)
set writebackup
" but do not leave around backup.xyz~ files after that
set nobackup
" backupcopy=yes is the default, just be explicit. We need this for
" webpack-dev-server and hot module reloading -- preserves special file types
" like symlinks
set backupcopy=yes

" don't create backups for these paths
set backupskip+=/tmp/*,$TMPDIR/*,$TMP/*,$TEMP/*
" Make Vim able to edit crontab files again.
set backupskip+=/private/tmp/*"
set backupskip+=~/.secret/*

" undo files
" double slash means create dir structure to mirror file's path
set undofile
set undolevels=1000
set undoreload=10000

" ----------------------------------------------------------------------------
" Spellcheck
" ----------------------------------------------------------------------------

" Add symlinked aspell from dotfiles as default spellfile
"let &g:spellfile = glob(expand(dko#vim_dir . '/en.utf-8.add'))

" ----------------------------------------------------------------------------
" Built-in completion
" ----------------------------------------------------------------------------

" Don't consider = symbol as part filename.
set isfname-==

set complete-=i                       " don't complete includes
set complete-=t                       " don't complete tags
set completeopt-=preview              " don't open scratch (e.g. echodoc)
set completeopt+=menu,menuone         " show PUM, even for one thing

" ----------------------------------------------------------------------------
" Message output on vim actions
" ----------------------------------------------------------------------------

set shortmess-=f                      " (file x of x) instead of just (x of x)
set shortmess+=ilmnrxoOtWI
" Disable "Pattern not found" messages
if has('patch-7.4.314') | set shortmess+=c | endif

" ----------------------------------------------------------------------------
" Window splitting and buffers
" ----------------------------------------------------------------------------

set splitbelow
set splitright
set fillchars=vert:│                  " Vertical sep between windows (unicode)

set hidden                            " remember undo after quitting

" reveal already opened files from the quickfix window instead of opening new
" buffers
set switchbuf=useopen

set nostartofline                     " don't jump to col1 on switch buffer

" ----------------------------------------------------------------------------
" Code folding
" ----------------------------------------------------------------------------

set foldlevelstart=99                 " show all folds by default
set nofoldenable

" ----------------------------------------------------------------------------
" Trailing whitespace
" ----------------------------------------------------------------------------

set list
set listchars=
set listchars+=tab:→\ 
set listchars+=trail:·
set listchars+=extends:»              " show cut off when nowrap
set listchars+=precedes:«
set listchars+=nbsp:⣿

" maybe...
" if has('patch-7.4.785') | set fixendofline | endif

" ----------------------------------------------------------------------------
" Diffing
" ----------------------------------------------------------------------------

" Note this is += since fillchars was defined in the window config
set fillchars+=diff:⣿
set diffopt=vertical                  " Use in vertical diff mode
set diffopt+=filler                   " blank lines to keep sides aligned
set diffopt+=iwhite                   " Ignore whitespace changes

" ----------------------------------------------------------------------------
" Input auto-formatting (global defaults)
" Probably need to update these in after/ftplugin too since ftplugins will
" probably update it.
" ----------------------------------------------------------------------------

set formatoptions=
set formatoptions+=c                  " Auto-wrap comments using textwidth
set formatoptions+=r                  " Continue comments by default
set formatoptions-=o                  " do not continue comment using o or O
set formatoptions+=q                  " continue comments with gq
set formatoptions-=a                  " auto-gq on type in comments?
set formatoptions+=n                  " Recognize numbered lists
set formatoptions+=2                  " Use indent from 2nd line of a paragraph
set formatoptions-=l                  " break lines that are already long?
set formatoptions+=1                  " Break before 1-letter words
" Vim 7.4 only: no // comment when joining commented lines
if v:version >= 704 | set formatoptions+=j | endif

set nrformats-=octal                  " never use octal when <C-x> or <C-a>

" ----------------------------------------------------------------------------
" Whitespace
" ----------------------------------------------------------------------------

set nowrap
set nojoinspaces                      " J command doesn't add extra space

" ----------------------------------------------------------------------------
" Indenting - overridden by indent plugins
" ----------------------------------------------------------------------------

set autoindent                        " indent when creating newline

" For autoindent, use same spaces/tabs mix as previous line, even if
" tabs/spaces are mixed. Helps for docblock, where the block comments have a
" space after the indent to align asterisks
"
" The test case what happens when using o/O and >> and << on these:
"
"     /**
"      *
"
" Refer also to formatoptions+=o (copy comment indent to newline)
set nocopyindent

" Try not to change the indent structure on "<<" and ">>" commands. I.e. keep
" block comments aligned with space if there is a space there.
set nopreserveindent

" Smart detect when in braces and parens. Has annoying side effect that it
" won't indent lines beginning with '#'. Relying on syntax indentexpr instead.
" 'smartindent' in general is a piece of garbage, never turn it on.
set nosmartindent

" Global setting. I don't edit C-style code all the time so don't default to
" C-style indenting.
set nocindent

" ----------------------------------------------------------------------------
" Tabbing - overridden by editorconfig, after/ftplugin
" ----------------------------------------------------------------------------

" use multiple of shiftwidth when shifting indent levels.
" this is OFF so block comments don't get fudged when using ">>" and "<<"
set noshiftround

" When on, a <Tab> in front of a line inserts blanks according to
" 'shiftwidth'. 'tabstop' or 'softtabstop' is used in other places.
set smarttab

set backspace=indent,eol,start        " bs anything

" ----------------------------------------------------------------------------
" Match and search
" ----------------------------------------------------------------------------

set matchtime=1                       " tenths of a sec
set showmatch                         " briefly jump to matching paren?
set hlsearch
set incsearch
set wrapscan                          " Searches wrap around end of the file.
set ignorecase
" Follow smartcase and ignorecase when doing tag search
if exists('+tagcase') | set tagcase=followscs | endif
set smartcase

if !empty(dko#GetGrepper())
  let &g:grepprg = dko#GetGrepper().command . ' '
        \ . join(dko#GetGrepper().options, ' ')
  let &g:grepformat = dko#GetGrepper().format
endif

" ----------------------------------------------------------------------------
" Old vim
" ----------------------------------------------------------------------------

if !has('nvim') | call dko#old#Settings() | endif

" ----------------------------------------------------------------------------
" Syntax
" Needs to be in vimrc (or ftdetect) since syntax runs before ftplugin
" ----------------------------------------------------------------------------

" ----------------------------------------
" Filetype: markdown
" ----------------------------------------

" Variable to highlight markdown fenced code properly -- uses tpope's
" vim-markdown plugin (which is bundled with vim7.4 now)
" There are more syntaxes, but checking for them makes editing md very slow
let g:markdown_fenced_languages = [
      \ 'html',
      \ 'javascript', 'js=javascript', 'json=javascript',
      \ 'sass',
      \ ]

" ----------------------------------------
" Filetype: php
" ----------------------------------------

" Additional syntax groups for php baselib
let g:php_baselib = 1
" Highlight unclosed ([]) - from $VIMRUNTIME/syntax/php.vim
let g:php_parentError = 1
" Assume strings contain HTML
let g:php_htmlInStrings = 1

" $VIMRUNTIME/indent/php.vim and 2072/
" Don't indent after <?php opening
let g:PHP_default_indenting = 0
" Don't outdent the <?php tags to the first column
let g:PHP_outdentphpescape  = 0

" ----------------------------------------
" Filetype: python
" ----------------------------------------

" $VIMRUNTIME/syntax/python.vim
let g:python_highlight_all = 1

" ----------------------------------------
" Filetype: sh
" ----------------------------------------

" $VIMRUNTIME/syntax/sh.vim - always assume bash
let g:is_bash = 1

" ----------------------------------------
" Filetype: vim
" ----------------------------------------

" $VIMRUNTIME/syntax/vim.vim
" disable mzscheme, tcl highlighting
let g:vimsyn_embed = 'lpPr'

" ============================================================================
" Plugins
" ============================================================================

" ----------------------------------------------------------------------------
" Plugins: Disable distributed plugins
" To re-enable you have to comment them out (checks if defined, not if truthy)
" ----------------------------------------------------------------------------

let g:loaded_2html_plugin = 1
let g:loaded_getscriptPlugin = 1
let g:loaded_gzip = 1
let g:loaded_LogiPat = 1
let g:loaded_tarPlugin = 1
let g:loaded_zipPlugin = 1

" Upstream matchparen -- it is inaccurate.
let g:loaded_matchparen = 1

" used to download spellfile and enable gx mapping
"let g:loaded_netrwPlugin = 0

" ----------------------------------------------------------------------------
" Plugins: netrw
" ----------------------------------------------------------------------------

" netrw in details format when no vimfiler
let g:netrw_liststyle      = 3
let g:netrw_home           = expand(g:dko#vim_dir . '/.tmp/cache')
let g:netrw_browsex_viewer = has('mac') ? 'open' : 'xdg-open'

" ----------------------------------------------------------------------------
" Plugins: autoinstall vim-plug, define plugins, install plugins if needed
" ----------------------------------------------------------------------------

let s:has_plug = !empty(glob(expand(g:dko#vim_dir . '/autoload/plug.vim')))
" Load vim-plug and its plugins?
if !s:has_plug && executable('curl')
  call dkoplug#install#Install()
  let s:has_plug = 1
endif

if s:has_plug
  command! PI PlugInstall
  command! PU PlugUpgrade | PlugUpdate
  let g:plug_window = 'tabnew'
  call plug#begin(g:dko#plug_absdir)
  if empty($VIMNOPLUGS) | call dkoplug#plugins#LoadAll() | endif
  call plug#end()
endif

" ============================================================================
" Autocommands
" ============================================================================

" Window aucmds -- affect layout
augroup dkowindow
  autocmd!

  " Resize all windows when the window is resized
  autocmd VimResized
        \ *
        \ wincmd =

  " Quit if quickfix is the only window
  " from https://github.com/now/vim-quit-if-only-quickfix-buffer-left/blob/master/plugin/now/quit-if-only-quickfix-buffer-left.vim
  autocmd WinEnter
        \ *
        \ if winnr('$') == 1 && &buftype == 'quickfix' | quit | endif

  " auto open quickfix and loclist?
  autocmd QuickFixCmdPost
        \ [^l]*
        \ cwindow
  "autocmd   QuickFixCmdPost l*      lwindow
augroup END

augroup dkoproject
  autocmd!

  autocmd BufNewFile,BufRead,BufWritePost
        \ *
        \ call dkoproject#GetRoot()
augroup END

" WordPress VIP specific
augroup dkowordpresssucksdonkeyballs
  autocmd!

  " No EOL character on files -- specifically for WP VIP PHP
  autocmd BufNewFile,BufReadPre
        \ */wp-content/themes/vip/*
        \ setlocal noeol binary fileformat=dos

  " Indent case: and default: in switch()
  autocmd BufNewFile,BufReadPre
        \ */*content/themes/*,*/*content/plugins/*,*/mu-plugins/*
        \ let b:PHP_vintage_case_default_indent = 1
augroup END

" ============================================================================
" Security
" ============================================================================

" Disallow unsafe local vimrc commands
" Leave down here since it trims local settings
set secure
